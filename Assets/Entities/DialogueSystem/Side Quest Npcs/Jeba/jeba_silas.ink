INCLUDE ../../MainInkLibrary.ink

~temp activeQuests = get_activeQuestList()

{contains(activeQuests, "jeba_return"):
    ->jeba_return
-else:
    ->generic
}



=== jeba_return ===
Ну как, удалось ли найти всех троих?
    +[Да, они все были рады услышать новости от своего капитана.]
        Хорошо. Я рад, что они тоже с теплотой вспоминают старые добрые деньки.
        ~invoke_dialogue_event("jeba_return")
        Ты заслужил награду. Хоть даже если всё что ты сделал, это порадовал старого капитана...
        ->END

=== generic ===

{not check_if_quest_has_been_taken("jeba_first"):
    ->firstGreeting
-else:
    ->secondGreeting
}


=== firstGreeting ===
Эй, салага! Что привело тебя в это захолустье?
    +[Что-то не так?]
        Здесь так много людей, они сводят меня с ума...
        Единственное, что спасает - близость моря. Находясь возле берега, я снова чувствую себя молодым.
        Слышал ли ты когда-нибудь, человек, о великой команде корабля "Морской Орёл"? Я был капитаном этого корабля.
        Под моим чутким руководством мы вместе совершили кучу подвигов и выполнили множество важных задач.
        Что ж, то время давно уже прошло. Всё равно, члены моей команды и по сей день живут где-то в королевстве.
        Человек, если тебе знакома морская солидарность, навести моих напарников и узнай, как у них дела.
        Я знаю, где живут трое из них, самые славные ребята - отличный медик Руна, великолепный пловец Соломон и превосходный разведчик Лира.
            ++[Хорошо, я согласен.]
                Вот и замечательно. Дай сюда свою карту, я отмечу тебе, где их искать. 
                ~add_quest("jeba_first")
                ~add_quest("jeba_second")
                ~add_quest("jeba_third")
                Как поговоришь со всеми, возвращайся ко мне. Не задерживайся, я буду ждать тебя, сухопутный!
                ->END
            ++[Мне срочно нужно ехать по делам...]
                Не пойму, в чём смысл ездить по земле. То ли дело бороздить морские просторы...
                ->END
    +[Я лучше пойду.]
        Решил не связываться со старым королём морей? Что ж, умно...
        ->END


=== secondGreeting ===
{contains(activeQuests, "jeba_"):
    Ну как, навестил ли ты всех моих друзей?
        +[Я работаю над этим.]
            Поторопись, сухопутный...
            ->END
-else:
    Снова здравствуй, мой верный матрос! Когда-нибудь я вновь буду бороздить морские просторы.
    Мне понадобится команда. Поплывёшь вместе со мной? Конечно поплывёшь, я знаю!
    ->END
}