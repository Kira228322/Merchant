INCLUDE ../../MainInkLibrary.ink

~temp activeQuests = get_activeQuestList()

{not check_if_quest_has_been_taken("chain_give_amulet"):
    ->give_item
-else:
    ->generic
}

=== give_item ===
Здравствуй, добрый молодец! Как несчастная старушка вроде меня может помочь тебе?
	+[А почему вы несчастная?]
		Да откуда-ж тут счастью взяться то? Совсем одна осталася я на старости лет. Муженёк мой уж умер давно, а дети да внуки не приезжают... Оно и ладно, все ведь заняты...
		Одна у меня отрада осталась – в огороде работать. Что-то вырастет, часть сама съем, часть продам, вроде и хватает на жизнь.
		Беда только, какой-то жук завёлся, все мои грядки пожрал. Не знаю, чем его даже и травить. Этак я без урожая останусь. Мне бы найти какого-то умника или книжку...
		{contains(activeQuests, "chain_give_gardenbook"):
		++[Посмотрите, вот такая книга не подойдёт? *Отдать «Уход за огородом»*]
		Ой, внучок, кажется это то, что нужно! Ты погляди, тут всякие разные вредители и средства против них. Даже с картинками!
		Кажется, урожай мой будет спасён. Спасибо, милок, услужил бабушке на старости лет. Ой, как хорошо!
		~invoke_dialogue_event("chain_give_gardenbook")
		~place_item("Странный амулет", 1, 0)
		Милок, возьми в награду вот эту штуку. Я давеча нашла в огороде, ума не приложу, что это такое. Может, такому молодцу, как ты, будет полезно.
		Ну, спасибо тебе ещё раз. Редко когда встретишь кого-то с добрым сердцем!
		->END 
		}
		++[Звучит печально. Я постараюсь что-то найти для вас.]
		Да что ты, внучок, не утруждайся ради меня… Бабушка уже и так стара, побереги силы...
		->END
	+[Простите, мне нужно идти.]
		Иди, раз уж нужно, внучок. Мне идти-то уж некуда...
			->END

=== generic ===
Милок, снова здравствуй! Погляди, мой огород по-настоящему процветает! Всё благодаря тебе. Спасибо!
->END