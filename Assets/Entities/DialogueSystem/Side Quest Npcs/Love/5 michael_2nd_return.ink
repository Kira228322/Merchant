INCLUDE ../../MainInkLibrary.ink

~temp activeQuests = get_activeQuestList()

{contains(activeQuests, "love_report_fail"):
    -> fail
}
{contains(activeQuests, "love_get_letter"):
    -> success
}
{check_if_quest_has_been_taken("love_give_letter") && not contains(activeQuests, "love_give_letter"):
    ->aftermath
-else:
    -> generic
}

=== fail ===
Привет! Ну, как всё прошло?

+[Похоже, мы ошиблись с подарком. Ей не понравилось, и она прогнала меня. Извини.]
	Эх, вот оно как, значит...
	Не извиняйся, странник, это не твоя вина. Мне нужно было быть менее трусливым и самому к ней подойти.
	    ~invoke_dialogue_event("love_report_fail")
        Ладно уж, давай сюда эту слизь. Эх, какое несчастье...
	    ->END
	
=== success ===

Привет! Ну, как всё прошло?

+[Камилле понравился подарок. Она согласилась познакомиться с тобой.]
	Ура, ура! Я так счастлив! Сейчас же пойду и познакомлюсь!
	...
	Впрочем... знаешь, мне как-то неспокойно на душе. Вдруг я скажу что-нибудь не так?
	Сделай для меня ещё одно одолжение: я напишу письмо, а ты доставишь его ей. Так я смогу собраться с мыслями и правильно передать свои чувства. Пожалуйста!
	    ++[Ох, ладно, давай сюда.]
	        Отлично, спасибо большое!
	        ->a
	    ++[Какой же ты трусливый. Тебе не хватает уверенности!]
	        Эх, я это понимаю. Но ничего не могу с собой поделать!
	        ->a
=a
Вот письмо, просто отдай ей и скажи, что от меня.
~invoke_dialogue_event("love_get_letter")
~place_item("Письмо для Камиллы", 1, 0)
~change_player_money(143)
Ах да, и вот твоя награда за всё, что ты сделал для меня!
	->END
	
=== generic ===
Пожалуйста, доставь письмо поскорее. Это последнее, о чём я тебя прошу, честно!
->END

=== aftermath ===
А, это же мой добрый друг! Спасибо за твою помощь. У нас с Камиллой всё хорошо, мне очень нравится проводить с ней время. Спасибо тебе!
->END