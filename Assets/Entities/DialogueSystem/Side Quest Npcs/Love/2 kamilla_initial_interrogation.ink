INCLUDE ../../MainInkLibrary.ink

~temp activeQuests = get_activeQuestList()
{contains(activeQuests, "love_gather_intel"):
    ->interrogation
-else:
    ->generic
}

//0 Дерзкий
//  00 Мудак (фейл)
//  01 Бэд Бой
//      010 Быдло (фейл)
//      011 Мачо (успех)
//1 Галантный
//  10 Подлиза
//      100 Мямля (фейл)
//      101 Романтик (успех)
//  11 Джентльмен (успех)


=== interrogation ===
Здравствуйте. Я вас раньше здесь не видела. Чем могу помочь?
+ [Быстро говори, что хочешь в подарок?]
    Как вы разговариваете со мной? Что это вообще такое, кто вы?
        ++[Я не буду повторять дважды, ты, грязная девка! Отвечай!]
            Ах вы... Ах вы... Мерзавец, подлец, хамло! 
            Принесите мне кусок слизи, чтобы я могла кинуть им в ваше лицо! Уйдите с глаз моих! Убирайтесь!
            ~invoke_dialogue_event("love_gather_intel")
            ~add_quest("love_failed_get_present")
            ->END
        ++[*Уверенно взять за руку* Я тот, кто может сделать тебя счастливой.]
            Вот как? И как же это понимать?
                +++[Сделай для меня всё, что я пожелаю, а взамен проси любой подарок. Что бы ты хотела?]
                    Фу, пошляк! Я не такая! Да хоть кусок слизи, только отвяжитесь от меня, наглец! Какой ужас, какие здесь ужасные люди ходят!
                    ~invoke_dialogue_event("love_gather_intel")
                    ~add_quest("love_failed_get_present")
                    ->END
                +++[Прошу, мне всего лишь нужно знать твой любимый предмет. От этого многое зависит.]
		            Ой, ну я даже и не знаю... Ну, мармелад я кушать люблю. У нас в деревню редко привозят...
		            ~invoke_dialogue_event("love_gather_intel")
                    ~add_quest("love_successful_get_present")
                    ->END
+ [Добрый день, прекрасная леди. Как ваше самочувствие?]
    И вам не хворать, красноречивый незнакомец. А чему я обязана комплиментам?
        ++[Просто во всём мире я не встречал более прекрасной девушки, чем вы! Ваша красота величественнее звёзд!]
            Ну ладно, хватит пресмыкаться. Немедленно говорите, что хотели!
            +++[Да я... эм... в общем, подарок предложить...]
                Ничего мне не надо от вас. 
                Дайте кусок слизи, чтобы я втирала его себе в кожу и стала жабой, чтобы подлизы вроде вас не приставали!
                ~invoke_dialogue_event("love_gather_intel")
                ~add_quest("love_failed_get_present")
                ->END
            +++[Назовите ваш любимый предмет, и вы получите его с первыми лучами солнца!]
                А вы всё-таки романтик... Я бы хотела баночку мармелада, очень уж я его люблю.
                ~invoke_dialogue_event("love_gather_intel")
                ~add_quest("love_successful_get_present")
                ->END
        ++[Комплименты вполне заслужены. Как насчёт подарка, чтобы сгладить первое впечатление?]
            Что ж, вы создаёте впечатление настоящего джентльмена, что большая редкость в нашей дыре.
            Я очень люблю мармелад. Так жаль, что его редко сюда привозят...
            ~invoke_dialogue_event("love_gather_intel")
            ~add_quest("love_successful_get_present")
            До свидания, надеюсь, мы встретимся вновь!
            ->END

=== generic ===
Доброго дня, молодой человек.
->END