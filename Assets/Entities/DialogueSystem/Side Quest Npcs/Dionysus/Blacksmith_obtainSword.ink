INCLUDE ../../MainInkLibrary.ink


Чего тебе?

~temp questSummaries = get_activeQuestList()

{contains(questSummaries, "the_start_of_honor_free_opportunity"):
    -> talk_about_sword
-else:
    ->generic
}

=== talk_about_sword ===
    +[Это у тебя Дионис заказал серебряный меч?]
        Ну да, только я не виноват, что он оказался обычным нищим бродягой.
        Он отказался платить, когда узнал, сколько мне должен. Какие-то проблемы?
            ++[Почему ты сразу не назвал окончательную цену?]
                По-твоему, я дурак? Меч получился высочайшего качества. За такое качество нужно платить, так работают торговые отношения.
                    ->negotiations        
    
=== generic ===
Чего надо? Если хочешь что-то купить, давай торговаться.
    +[Мне пора идти.]
        Ну иди, я не держу.
        ->END
    
=== negotiations ===

Что ж, если у тебя есть какие-то предложения, то я их выслушаю. Если нет, то не мешай работать.

+[Ты не боишься потерять свою работу, так обращаясь с клиентами?]
    Что это должно значить?
        ++[Например, бандиты могут узнать, что в этой деревне живет жадный кузнец...]
            Ах ты подлец! Будешь меня шантажировать? Это тебе стоит ходить и оглядываться, а не мне!
            ~invoke_dialogue_event("silver_sword_declined")
            Или ты платишь полную стоимость, или уходи. Больше нам не о чём говорить.
            ->END
            
        ++[Я же вижу, что у тебя ещё осталась совесть. Давай пересмотрим цену]
            ->negotiations_half_success
            
        ++[Что, если к тебе никто не будет ходить, узнав о твоей жадности...?]
            К сожалению для тебя, пацан, это так не работает. Я мастер своего дела, люди всегда будут ко мне обращаться.
            ~invoke_dialogue_event("silver_sword_declined")
            Я считаю, тут больше нечего обсуждать. Цена останется прежней.
            ->END
            
+[Ну давай подумаем вместе: почему ты решил, что цена слишком низка?]
    Ну, потому что я пересчитал, сколько стоили материалы и учёл время, затраченное на ковку меча.
    Это оказалось сложнее, чем я ожидал, и я решил поднять цену.
        ++[Но когда вы договаривались с Дионисом, разве эти факторы не учитывались?]
            ->negotiations_half_success
            
        ++[Неужели ты не понимаешь, что Дионис беден, и он не сможет тебе заплатить?]
            Я не вижу, в каком месте это моя проблема.
            ~invoke_dialogue_event("silver_sword_declined")
            По тебе ясно, что у тебя деньги водятся. Значит, ты сможешь легко купить у меня этот меч.
            Этот разговор окончен.
            ->END

=== negotiations_half_success ===

Ну пойми меня, у меня есть дети, которых нужно кормить. Эти деньги мне тоже нужны, как и Дионису.
Хоть я и не хотел завышать цену, мне пришлось это сделать.
    +[Что же, счастье детей для тебя важнее чести?]
        Как ты смеешь такое говорить! Я всю жизнь был солдатом.
        ~invoke_dialogue_event("silver_sword_declined")
        Честь важна, но разве можно назвать меня честным, если я не могу обеспечить свою семью?
        Уходи, путник. Нам не о чем говорить.
        ->END
        
        
    +[С помощью этого меча Дионис, возможно, спасёт не одну жизнь.]
        ...
        ~place_item("Серебряный меч", 1, 0)
        ~invoke_dialogue_event("silver_sword_obtained")
        Ах, ладно! Забирай этот меч, убедил.
            ->END
