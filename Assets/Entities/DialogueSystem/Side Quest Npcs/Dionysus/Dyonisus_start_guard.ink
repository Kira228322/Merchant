INCLUDE ../../MainInkLibrary.ink

~temp activeQuests = get_activeQuestList()

{contains(activeQuests, "dyonisus_guard_village"):
-> in_progress
}
{contains(activeQuests, "dyonisus_return_after_guard"):
-> finished
}
{check_if_quest_has_been_taken("dyonisus_guard_village"):
-> already_done
-else:
-> new_quest
}

=== new_quest ===
Эй, давно не виделись!
    +[Как дела? Ты проводил того человека?]
        Да, мы сразу привезли сюда заодно и его больного родственника. Сейчас они у лекаря. Однако с обратной дорогой возникли проблемы...
		Ходят слухи, что бандиты хотят атаковать эту деревню. Я решил остаться и поохранять окрестности.
        Может быть, от меня одного будет мало толку, но, скорее всего, бандиты побоятся нападать, если увидят людей, способных дать отпор.
		Ты не поможешь? Нужно просто поглядывать по сторонам и поднять тревогу, если что-то увидишь. Всё-таки делаем доброе дело.
		++[Хорошо, я с тобой.]
			Ну и отлично! Знаю, сражаться ты не любишь, но если что, просто зови меня.
			~add_quest("dyonisus_guard_village")
			Разделимся – я буду сторожить вот ту часть деревни, а ты – вот эту. Спасибо, что согласился!
			Только никуда не уезжай, а то твоя часть деревни останется без присмотра!
			->END 
		++[Нет, я пас.]
			Понимаю, всё-таки ты не любишь драться. Тогда просто будь осторожнее на дороге, береги себя.
			->END

    +[Я вернусь позже.]
        Конечно, я всегда рад тебя видеть, дружище!
        ->END
=== in_progress ===
Ну как, видел кого-нибудь подозрительного?
	+[Нет, всё чисто.]
		Хорошо. Продолжаем глядеть в оба! Благополучие жителей этой деревни в наших руках!
		->END

=== finished ===
И снова здравствуй, друг мой! Видел что-нибудь?
    +[Совсем ничего. Всё чисто.]
        Что ж, бандиты не показали носа. Думаю, узнали про нас с тобой и испугались, ха-ха-ха!
        ~invoke_dialogue_event("dyonisus_return_after_guard")
        Хорошая работа!
        ->END
=== already_done ===
Рад видеть тебя! Как жизнь?
    +[Всё хорошо. Мне нужно идти.]
        Пока и удачи!
        ->END