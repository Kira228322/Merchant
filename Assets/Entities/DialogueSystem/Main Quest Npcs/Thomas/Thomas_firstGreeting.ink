INCLUDE ../../MainInkLibrary.ink

~temp questSummaries = get_activeQuestList()
{contains(questSummaries, "tutorial_talk_to_richard"):
-> farewell
}
{contains(questSummaries, "tutorial_talk_to_thomas_after_reading_letter"):
-> goToRichard
}
{contains(questSummaries, "tutorial_readletter"):
-> readLetter
}
-> firstGreeting



=== firstGreeting ===
А, вот ты и приехал! Добро пожаловать в нашу деревню. Ты меня не помнишь?
Я Томас, мы с твоим отцом старые друзья. Последний раз я тебя видел, когда ты был ещё совсем мал, ха-ха!
Так что можешь называть меня "дядя Том". В любом случае, я здесь, потому что твой отец попросил тебя встретить. Ты же получил письмо от него?
Он сам куда-то пропал пару дней назад. Остаётся верен своей привычке нигде не задерживаться надолго.
Ты же про него почти ничего не знаешь, так ведь?
    +[Я знаю, что он странствующий торговец.]
        Да, в целом так и есть. Обычно он ездит по всему нашему королевству, а иногда даже за границу.
        Чудной у тебя отец, но за это я всегда его и уважал, ха-ха-ха!
        -> cut_to_the_chase
    +[Я знаю, что он бросил нас с матерью и уехал!]
        Ну-ну, не горячись. Я понимаю, как это выглядит, но мне кажется, тогда это была необходимая мера.
        Мне он очень часто рассказывает, как вы ему дороги.
        Всё-таки торговлей он зарабатывает на жизнь, в том числе и для тебя с матерью.
        -> cut_to_the_chase
        
= cut_to_the_chase
Ну да ладно, перейдем к делу.
Перед тем как куда-то уйти, твой отец приехал ко мне. Он выглядел достаточно обеспокоенным.
Он сказал, что попросил тебя приехать и оставил здесь свою повозку и какие-то вещи.
Ещё он просил передать тебе вот этот конверт.

~place_item("Конверт от отца", 1, 0)
~add_quest("tutorial_readletter")
\* Томас вручает вам конверт... \*
-> END

=== readLetter ===
Ну же, прочитай письмо из конверта. Я не вскрывал, так что мне самому интересно, что там!
-> END

== farewell ===
Счастливого пути!
->END

=== goToRichard ===
Ну как, что говорилось в письме?
    +[Мне нужно найти какого-то Ричарда...]
        Ричарда? Да, слышал про такого. На самом деле, неприятный старик.
        Деньги любит больше всего на свете, очень скуп и дерзок.
        Он живёт в соседней деревне под названием Сосновка, на этой повозке легко доедешь.
        ~invoke_dialogue_event("tutorial_talk_to_thomas_after_reading_letter")
        Ну что ж, удачи тебе и счастливого пути!
        ->END
