INCLUDE ../../../MainInkLibrary.ink

~temp activeQuests = get_activeQuestList()

{contains(activeQuests, "tradepath_ask_for_government_approval"):
    ->ask_for_approval
-else:
    ->generic
}

=== ask_for_approval ===
\* Женщина-клерк из Министерства экономики занята тем, что перекладывает какие-то бумаги с места на место.*
    +[Здравствуйте. Я хотел оставить заявку на открытие торгового пути.]
        Вы что, не видите, что я работаю! Подождите пять минут.
        \*Вы терпеливо ждёте. Проходит пять, затем десять минут. В поведении клерка ничего не изменяется. Вы решаете напомнить о своём существовании...*
            ++[Гхм-гхм! Прошу прощения, вы не могли бы меня выслушать?]
                Невероятно, какие хамы приходят к нам! Ну что у вас?
                    +++[Как я уже сказал, хочу оставить заявку на открытие торгового пути.]
                        Заявки граждан рассматриваются только по чётным дням нечётных недель, с шести до восьми утра. Приходите в другое время!
                            ++++[Я работаю от лица Гильдии торговцев. Карлос Золотой Рукав очень расстроится, если его дела пойдут не по плану...]
                                Что ж вы сразу не сказали.. У нас особые квоты для членов гильдии...
                                ->continue
                            ++++[Послушайте, я понимаю, вы устали. К вам приходят неблагодарные люди и хамят вам. Но может быть ради меня всё-таки сделаете исключение?]
                                Что ж, по крайней мере, вы обходительнее, чем многие.
                                ->continue
                            ++++[Я не буду слушать ваши глупые отговорки. Немедленно начните выполнять свою работу, прежде чем я вышел из себя!]
                                Вот только угрожать мне тут не надо. Я таких как вы, каждый день вижу!
                                ->continue
=continue
Вот заявка, заполняйте. Писать хоть умеете?
\*Вы аккуратно заполняете заявку, стараясь не допустить ни единой ошибки...*
Давайте сюда. Она будет отправлена на рассмотрение в Министерство Экономики.
~invoke_dialogue_event("tradepath_ask_for_government_approval")
Возвращайтесь позже, минимум через два дня. Может быть вам повезёт и её рассмотрят быстро.
    +[Спасибо.]
        До свидания.
            ->END
    +[Эх, во что я ввязался...]
        До свидания.
            ->END


=== generic ===
Что вы хотите? Я же сказала вам, приходите позже!
    ->END