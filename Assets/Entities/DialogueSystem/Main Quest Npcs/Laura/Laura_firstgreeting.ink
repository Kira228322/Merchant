INCLUDE ../../MainInkLibrary.ink

~temp questSummaries = get_activeQuestList()
{contains(questSummaries, "laura_firstGreeting"):
    { not check_if_quest_has_been_taken("asking_about_bandit_camp"):
    ->firstGreeting
    }
    ->generic
}
->tell_off


=== firstGreeting ===
Эй, ты что, снова один из этих? Я же сказала, дайте мне немного времени!

    +[Что? Про кого ты говоришь?]
        А, так ты не бандит? Фух, как гора с плеч!
        Что ж, я прошу прощения. Просто я сейчас в неприятной ситуации, и я подумала...
            ++[Меня прислал Марк. Мой отец работал вместе с вами. Я вместо него.]
                    Погоди-погоди, а что с ним случилось?
                        +++[*Рассказать про ситуацию с отцом*]
                            Какой ужас! Так значит, бандиты добрались не только до меня.
                                -> aboutProblems
                
=== aboutProblems ===
    Немного ранее ко мне пришла шайка этих бандитов. Они расспрашивали про реликвию, которую мне должен был привезти твой отец. 
    Они перевернули мой дом вверх дном и грозились пытать меня.
    Сказали, что у меня есть время на размышления и обещали скоро вернуться.
        +[Ты ведь говоришь про эту реликвию? *Показать вещь, купленную у Ричарда*]
            Да, это она. Только вот бандиты могут вернуться в любой момент... Что ж, давай её сюда, я постараюсь спрятать её куда-нибудь.
                ++[Почему ты просто не можешь отдать им реликвию?]
                    Нет, ты что, конечно не могу! Твой отец пострадал из-за неё, а ты хочешь просто её отдать?
                    Кроме того, он говорил, что она крайне важна. Нет, отдавать её нельзя, но нужно придумать какой-то способ избавиться от этих бандитов...
                    Жаль, что мы не знаем где у них лагерь. Если бы знали, тогда можно было бы сообщить страже. Они знают своё дело, хоть и не пошевелятся, пока не дашь им денег.
                    Впрочем, я знаю хорошего капитана стражи, её зовут Селена. Она честная и имеет доброе сердце, не то что остальные. Думаю, стоит поговорить с ней.
					~invoke_dialogue_event("laura_firstGreeting")
                    +++[Я порасспрашиваю в округе.]
                        ->generic
                        
                ++[Что нам делать?]
                    Я думаю, нужно узнать, где лагерь у этих бандитов, а потом сообщить страже.
                    Нельзя надеяться на их расторопность или желание выполнять свою работу, но обычно они работают активнее, если пообещать им золота...
                    Впрочем, я знаю хорошего капитана стражи, её зовут Селена. Она честная и имеет доброе сердце, не то что остальные. Думаю, стоит поговорить с ней.
					~invoke_dialogue_event("laura_firstGreeting")
                    +++[Хорошо, я займусь этим.]
                        ->generic
                      
=== tell_off ===
Я сейчас занята, извини.
->END
                    
=== generic ===
Ты хотел что-то спросить?
    +[Расскажи о себе.]
        Особо нечего рассказывать. Я - свободный маг, люблю изучать что-то новое и смотреть на всякие диковинки.
        Недавно окончила магическую академию и съехала от родителей. Теперь пока кое-как справляюсь сама.
        ->generic
    +[Как ты познакомилась с моим отцом?]
        Очень просто - однажды я гуляла по городской площади и увидела его телегу с товарами.
        Меня сразу заинтересовали редкие магические товары, привезённые из далёких земель.
        Мы быстро подружились - он рассказывал диковинные истории, а я внимательно слушала.
        Однажды он предложил помочь ему с исследованием магических реликвий, а я была только рада!
        ->generic
    +[Нет, я пойду.]
        Удачи!
        ->END